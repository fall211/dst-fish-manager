#!/usr/bin/env fish
# One-shot updater for DST.

set -l CONFIG_FILE "$HOME/.config/dontstarve/config"
if test -f "$CONFIG_FILE"
    for line in (cat $CONFIG_FILE | string match -r '^[^#].*=.*')
        set -l parts (string split -m 1 "=" $line)
        set -l key (string trim $parts[1])
        set -l value (string trim -c '"' $parts[2] | string replace -r '^\$HOME' "$HOME" | string replace -r '^~' "$HOME")
        set -gx $key $value
    end
end

# Default values
set -q INSTALL_DIR; or set -gx INSTALL_DIR "$HOME/dontstarvetogether_dedicated_server"
set -q STEAMCMD_DIR; or set -gx STEAMCMD_DIR "$HOME/steamcmd"
set -l APP_ID 343050

function fail
    echo "Error: $argv" >&2
    exit 1
end

if not command -v systemctl >/dev/null
    fail "systemctl not found. This script requires systemd."
end

if test (id -u) -eq 0
    fail "This script should not be run as root. Run it as a regular user."
end

if not test -x "$STEAMCMD_DIR/steamcmd.sh"
    fail "steamcmd not found at $STEAMCMD_DIR/steamcmd.sh"
end

if not test -d "$INSTALL_DIR"
    fail "install dir missing: $INSTALL_DIR"
end

# Stopping servers before update
if systemctl --user is-active --quiet dontstarve.target
    echo "Stopping dontstarve.target (user)"
    systemctl --user stop dontstarve.target
end

echo "Running steamcmd update..."
# Note: we don't use 'validate' to avoid overwriting mods
if "$STEAMCMD_DIR/steamcmd.sh" +force_install_dir "$INSTALL_DIR" +login anonymous +app_update "$APP_ID" +quit
    echo "Update completed successfully."
else
    echo "An error occurred during update. Starting dontstarve.target to be safe." >&2
end

# Restarting servers
echo "Starting dontstarve.target (user)"
systemctl --user start dontstarve.target
